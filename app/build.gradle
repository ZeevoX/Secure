/*
 * Copyright (C) 2018 Timothy "ZeevoX" Langer
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file
 * except in compliance with the License. You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the
 * License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the specific language governing
 * permissions and limitations under the License.
 */

import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

static def buildDate() {
    def df = new SimpleDateFormat("yyyyMMdd")
    return df.format(new Date())
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        // All builds must be signed with this key in order for Google Drive backup to work
        debug {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    dataBinding {
        enabled = true
    }

    lintOptions {
        checkReleaseBuilds false
        disable 'Deprecation'
        abortOnError false
    }

    compileSdkVersion 29

    defaultConfig {
        applicationId "com.zeevox.secure"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 5
        versionName "0.4 beta"

        android.applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "Secure-${buildDate()}-${versionName}.apk"
            }
            /*if (variant.buildType.name == "release") { variant.outputs.each { } }*/
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            versionNameSuffix '.debug'
            debuggable true
            jniDebuggable true
            renderscriptDebuggable true
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.google.android.material:material:1.1.0-alpha09'
    implementation 'androidx.appcompat:appcompat:1.1.0-rc01'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-beta03'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.core:core-ktx:1.2.0-alpha03'
    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    implementation 'com.google.http-client:google-http-client-gson:1.31.0'
    //implementation 'com.google.api-client:google-api-client:1.30.2'
    implementation 'com.google.api-client:google-api-client-android:1.30.2'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.30.1'
    implementation 'com.google.apis:google-api-services-drive:v3-rev173-1.25.0'
}

repositories {
    mavenCentral()
}
